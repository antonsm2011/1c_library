////////////////////////////////////////////////////////////////////////////////
//                   МОДУЛЬ ФОРМЫ ВЫБОРА ПОЛЬЗОВАТЕЛЯ WINDOWS                 //
////////////////////////////////////////////////////////////////////////////////

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	#Если ТолстыйКлиентОбычноеПриложение ИЛИ ТолстыйКлиентУправляемоеПриложение Тогда
	ТаблицаДоменовИПользователей = ПользователиОС();
	#ИначеЕсли ТонкийКлиент Тогда
	ТаблицаДоменовИПользователей = Новый ФиксированныйМассив (ПользователиОС());
	#КонецЕсли
	
	ЗаполнитьСписокДоменов();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокДоменов ()
	
	СписокДоменов.Очистить();
	
	Для Каждого Запись Из ТаблицаДоменовИПользователей Цикл
		Домен = СписокДоменов.Добавить();
		Домен.ИмяДомена = Запись.ИмяДомена;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДоменовПриАктивизацииСтроки(Элемент)
	
	ИмяДомена = Элемент.ТекущиеДанные.ИмяДомена;
	
	Для Каждого Запись Из ТаблицаДоменовИПользователей Цикл
		Если Запись.ИмяДомена = ИмяДомена Тогда
			СписокПользователейТекущегоДомена.Очистить();
			Для Каждого Пользователь Из Запись.Пользователи Цикл
				ПользовательДомена = СписокПользователейТекущегоДомена.Добавить();
				ПользовательДомена.ИмяПользователя = Пользователь;
			КонецЦикла;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПользователейДоменаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СкомпоноватьРезультатИЗакрытьФорму();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОкВыполнить()
	
	ИмяДомена = Элементы.ТаблицаДоменов.ТекущиеДанные.ИмяДомена;
	ИмяПользователя = Элементы.ТаблицаПользователейДомена.ТекущиеДанные.ИмяПользователя;
	
	Если СокрЛП(ИмяДомена) <> "" И СокрЛП(ИмяПользователя) <> "" Тогда
		СкомпоноватьРезультатИЗакрытьФорму();
	КонецЕсли;
	
КонецПроцедуры

// Компонует результат выбора в виде строки \\ДОМЕН\ИМЯ_ПОЛЬЗОВАТЕЛЯ_ДОМЕНА
// и закрывает форму, возвращая это значение, в качестве результата работы формы.
//
&НаКлиенте
Процедура СкомпоноватьРезультатИЗакрытьФорму()
	
	ИмяДомена = Элементы.ТаблицаДоменов.ТекущиеДанные.ИмяДомена;
	ИмяПользователя = Элементы.ТаблицаПользователейДомена.ТекущиеДанные.ИмяПользователя;
	СтрокаПользователяWindows = "\\" + ИмяДомена + "\" + ИмяПользователя;
	Закрыть(СтрокаПользователяWindows);
	
КонецПроцедуры
