// Проверим не повторяются ли книги в ТЧ
Функция ПроверитьНаОдинаковыеПозиции(ТабЧасть, Отказ) Экспорт
	СтрокаНомер = 0;
	Для Каждого Строка из ТабЧасть цикл
		СтрокаНомер = СтрокаНомер + 1;
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Книга", Строка.Книга);
		Если ТабЧасть.НайтиСтроки(ПараметрыОтбора).Количество() > 1 Тогда
			Сообщить("Имеются одинаковые позиции книг. Строка: " + СтрокаНомер +". Проведение отменено!", СтатусСообщения.Важное);
			Отказ = Истина;
			Возврат Ложь;			
		КонецЕсли;		
	КонецЦикла;
	Возврат Истина;
КонецФункции

// Проверим остатки книг перед вводом остатков, выдет ошибку если такая книга уже есть
&НаСервере
Функция ПроверитьОстаткиНаОтсутствие(ЭтотОбъект, Отказ) Экспорт
	Запрос = новый запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЕСТЬNULL(КнигиВБиблиотекеОстатки.КоличествоОстаток, 0) КАК КоличествоРегистр,
	               |	ВложенныйЗапрос.Количество КАК КоличествоДокумент,
	               |	ВложенныйЗапрос.Книга,
	               |	ВложенныйЗапрос.НомерСтроки
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		1 КАК Количество,
	               |		ВводОстатковКнигКниги.Книга КАК Книга,
	               |		ВводОстатковКнигКниги.НомерСтроки КАК НомерСтроки
	               |	ИЗ
	               |		Документ.ВводОстатковКниг.Книги КАК ВводОстатковКнигКниги
	               |	ГДЕ
	               |		ВводОстатковКнигКниги.Ссылка = &Ссылка) КАК ВложенныйЗапрос
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.КнигиВБиблиотеке.Остатки(
	               |				&ДатаКон,
	               |				Книга В
	               |					(ВЫБРАТЬ
	               |						ВыдачаКнигКниги.Книга КАК Книга
	               |					ИЗ
	               |						Документ.ВводОстатковКниг.Книги КАК ВыдачаКнигКниги
	               |					ГДЕ
	               |						ВыдачаКнигКниги.Ссылка = &Ссылка)) КАК КнигиВБиблиотекеОстатки
	               |		ПО ВложенныйЗапрос.Книга = КнигиВБиблиотекеОстатки.Книга";
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДатаКон", ЭтотОбъект.Дата);
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Для каждого Строка из РезультатЗапроса цикл
		Если Строка.КоличествоРегистр >= Строка.КоличествоДокумент Тогда
			Сообщить("Уже введены остатки по позиции: "+ Строка.Книга+ " в строке: " + Строка.НомерСтроки + " Проведение отменено!", СтатусСообщения.Важное);		
			Отказ = Истина;
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	Возврат Истина;
КонецФункции

// Проверить установлена ли дата выдачи если расположение у абонента. Нужна для документа ввода остатков
&НаСервере
Функция ПроверитьДатуВыдачи(ЭтотОбъект, Отказ) Экспорт
	Для Каждого Строка из ЭтотОбъект.Книги цикл
		Если Строка.МестоРазмещения <> Справочники.МестаРазмещения.Библиотека Тогда			
			Если НЕ ЗначениеЗаполнено(Строка.ДатаВыдачи) Тогда
				Сообщить("В строке: " + Строка.НомерСтроки + " не указана дата выдачи");
				Отказ = истина;				
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Возврат Отказ;
КонецФункции

// В строку записываем авторов книги и передаем как результат
&НаСервере
Функция ПолучитьСтрокуАвторов(СсылкаНаКнигу) Экспорт
	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КнигиАвторы.Автор
	|ИЗ
	|	Справочник.Книги.Авторы КАК КнигиАвторы
	|ГДЕ
	|	КнигиАвторы.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаКнигу);
	
	Результат = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	
	СтрокаАвторов = "";	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ЗначениеЗаполнено(СтрокаАвторов) Тогда
			СтрокаАвторов = СтрокаАвторов + ", ";	
		КонецЕсли;
		СтрокаАвторов = СтрокаАвторов + ВыборкаДетальныеЗаписи.Автор;	
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	Возврат СтрокаАвторов;
КонецФункции

// Функция формирует получает максимальынй инвентраный номер
&НаСервере
Функция ПолучитьМаксимальныйИнвентарныйНомерКниги(КнигиСсылка) Экспорт
	
	ИнвентарныйНомер = "";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Книги.ИнвентарныйНомер КАК ИнвентарныйНомер
	|ИЗ
	|	Справочник.Книги КАК Книги
	|ГДЕ
	|	Книги.Ссылка <> &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИнвентарныйНомер УБЫВ";
	
	Запрос.УстановитьПараметр("Ссылка", КнигиСсылка);
	Результат = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ИнвентарныйНомер = ВыборкаДетальныеЗаписи.ИнвентарныйНомер;
	КонецЦикла;
	
	Возврат ИнвентарныйНомер;
	
КонецФункции

//проверме остатки книг перед списанием
//НаДату - дата на которую нужно знать остатки
//МассивСсылокКниг - массив типа СправочникСсылка.Книги содержит книги которые надо узнать имеются ли в библиотеке
&НаСервере
Функция ПроверитьОстаткиВБибиотеке(Отказ, НаДату, МассивСсылокКниг)	Экспорт 
	Запрос = новый запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	КнигиВБиблиотекеОстатки.КоличествоОстаток,
	               |	КнигиВБиблиотекеОстатки.Книга,
	               |	КнигиВБиблиотекеОстатки.МестоРазмещения
	               |ИЗ
	               |	РегистрНакопления.КнигиВБиблиотеке.Остатки(
	               |			&ДатаКон,
	               |			Книга В (&МассивКниг)
	               |				И МестоРазмещения <> ЗНАЧЕНИЕ(Справочник.МестаРазмещения.Библиотека)) КАК КнигиВБиблиотекеОстатки";
	Запрос.УстановитьПараметр("МассивКниг", МассивСсылокКниг);
	Запрос.УстановитьПараметр("ДатаКон", НаДату);	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	МассивРезультата = Новый Массив; //содержит массив книг которых нет в библиотеке
	Если РезультатЗапроса.Количество() > 0 Тогда
		Отказ = Истина;
		Пока РезультатЗапроса.Следующий() цикл		
			МассивРезультата.Добавить(РезультатЗапроса.Книга);					
		КонецЦикла;
	КонецЕсли;	
	Возврат МассивРезультата;
КонецФункции

// Получает значение константы, необходимо например для получения значения на клиенте
//
// Параметры
//  <ИмяКонстанты>  - <Строка> - <описание параметра>
//
// Возвращаемое значение:
//   <Произволный>   - <значение константы>
//
Функция ПолучитьЗначениеКонстанты(ИмяКонстанты) Экспорт

	Возврат Константы[ИмяКонстанты].Получить();

КонецФункции // ПолучитьЗначениеКонстанты()

// Делает запись об ошибке в журнал регистрации
//
// Параметры:
//  КраткоеОписаниеОшибки 	- Строка - Короткое описание ошибки
//  ОбъектМетаданных 		- Метаданные - Метаданные в коорых произошла ошибка
//  Данные 					- Ссылочные типы - Ссылка на объект. связанный с ошибкой
//  ОписаниеОшибки 			- Строка - Описание ошибки
//
Процедура ЗафиксироватьОшибку(КраткоеОписаниеОшибки, ОписаниеОшибки, ОбъектМетаданных = Неопределено, Данные = Неопределено) Экспорт

	ЗаписьЖурналаРегистрации(КраткоеОписаниеОшибки, УровеньЖурналаРегистрации.Ошибка, ОбъектМетаданных, Данные, ОписаниеОшибки)

КонецПроцедуры // ЗафиксироватьОшибку()

// Функция - Загрузить файл по ссылке
//
// Параметры:
//  СсылкаНаФайл - Строка - Ссылка на файл в сети интернет
// 
// Возвращаемое значение:
//  Строка - Имя временного файла, который был скачан
//
Функция ЗагрузитьФайлПоСсылке(СсылкаНаФайл, РасширениеФайла = Неопределено) Экспорт
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла(РасширениеФайла);
	Попытка
		КопироватьФайл(СсылкаНаФайл, ИмяВременногоФайла);
	Исключение
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = "Не удалось загрузить файл по ссылке";
		СообщениеПользователю.Сообщить();
		ОписаниеОшибки = ОписаниеОшибки();
		ОбщегоНазначения.ЗафиксироватьОшибку(СообщениеПользователю.Текст, ОписаниеОшибки + Символы.ПС + "Ссылка: " + СсылкаНаФайл, Метаданные.РегистрыСведений.КаталогКниг, Неопределено);
		Возврат Неопределено;
	КонецПопытки;
	
	Файл = Новый Файл(ИмяВременногоФайла);
	
	Если Не Файл.Существует() Тогда
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = "Не удалось загрузить файл по ссылке";
		СообщениеПользователю.Сообщить();
		ОбщегоНазначения.ЗафиксироватьОшибку(СообщениеПользователю.Текст, "Ссылка: " + СсылкаНаФайл, Метаданные.РегистрыСведений.КаталогКниг, Неопределено);
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ИмяВременногоФайла
	
КонецФункции