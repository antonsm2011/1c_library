// Процедура очищает табличную часть и заполняет всеми книгами
&НаКлиенте
Процедура ЗаполнитьИзСправочника(Команда)
	
	Если Вопрос("Будет очищена табичная часть. Продолжить?", РежимДиалогаВопрос.ДаНет,0) = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	Объект.Книги.Очистить();
	ЗаполнитьТЧКниги();
	
КонецПроцедуры

// Процедура заполняет табличную часть документа книгами из справочника
&НаСервере
Процедура ЗаполнитьТЧКниги()
	
	Документ = РеквизитФормыВЗначение("Объект");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Книги.Ссылка
		|ИЗ
		|	Справочник.Книги КАК Книги";

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Строка = Документ.Книги.Добавить();
		Строка.Книга = ВыборкаДетальныеЗаписи.Ссылка;
		Строка.МестоРазмещения = Справочники.МестаРазмещения.Библиотека;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(Документ, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборКниг(Команда)
	
	ПараметрыПодбора = Новый Структура("МножественныйВыбор", Истина);
	ОткрытьФорму("Справочник.Книги.ФормаВыбора", ПараметрыПодбора, Элементы.Книги);
	
КонецПроцедуры

&НаКлиенте
Процедура КнигиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ВыбранныеКниги = РазложитьГруппыКниг(ВыбранноеЗначение);
	
	Для Каждого Книга Из ВыбранныеКниги Цикл
		НоваяСтрока = Объект.Книги.Добавить();
		НоваяСтрока.Книга = Книга;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РазложитьГруппыКниг(СписокКнигСГруппами)
	Возврат ОбщегоНазначенияСервер.РазложитьГруппыКниг(СписокКнигСГруппами);
КонецФункции