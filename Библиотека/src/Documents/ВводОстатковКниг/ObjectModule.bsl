#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда // Для работы толстого клиента https://its.1c.ru/db/v8std#content:680:hdoc

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// Проверка проведения:
	// 1. Проверить на повторяющиеся элементы в ТЧ
	ОбщегоНазначенияСервер.ПроверитьНаОдинаковыеПозиции(Книги, Отказ);
	// 2. Проверить нет ли уже таких остатков в библиотеке
	ОбщегоНазначенияСервер.ПроверитьОстаткиНаОтсутствие(ЭтотОбъект, Отказ);
	// 3. Проверить установлена ли дата выдачи если расположение у абонента
	ОбщегоНазначенияСервер.ПроверитьДатуВыдачи(ЭтотОбъект, Отказ);
	
	// Регистр КнигиВБиблиотеке Приход
	Движения.КнигиВБиблиотеке.Записывать = Истина;
	Для Каждого ТекСтрокаКниги Из Книги Цикл
		Движение = Движения.КнигиВБиблиотеке.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Книга = ТекСтрокаКниги.Книга;
		Движение.МестоРазмещения = Справочники.МестаРазмещения.Библиотека;
		Движение.Количество = 1;
		Движение.Операция = Перечисления.Операция.ПоступлениеКниг;
		// Если размещение книги у абонента, то сделаем расход
		Если ТекСтрокаКниги.МестоРазмещения <> Справочники.МестаРазмещения.Библиотека Тогда
			// Вычтем с библиотеки
			Движение = Движения.КнигиВБиблиотеке.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = ТекСтрокаКниги.ДатаВыдачи;
			Движение.Книга = ТекСтрокаКниги.Книга;
			Движение.МестоРазмещения = Справочники.МестаРазмещения.Библиотека;
			Движение.Количество = 1;
			Движение.Операция = Перечисления.Операция.ВыдачаКнигиАбоненту;
			// И добавим абоненту
			Движение = Движения.КнигиВБиблиотеке.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = ТекСтрокаКниги.ДатаВыдачи;
			Движение.Книга = ТекСтрокаКниги.Книга;
			Движение.МестоРазмещения = ТекСтрокаКниги.МестоРазмещения;
			Движение.Количество = 1;
			Движение.Операция = Перечисления.Операция.ВыдачаКнигиАбоненту;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
