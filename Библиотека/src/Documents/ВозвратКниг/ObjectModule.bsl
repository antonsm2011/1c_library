#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда // Для работы толстого клиента https://its.1c.ru/db/v8std#content:680:hdoc

// Проверим что бы был указан абонент
Функция ПроверитьШапкуДокумента(Отказ)
	
	Если НЕ ЗначениеЗаполнено(Абонент)Тогда
		Сообщить(НСтр("ru = 'Не указано лицо от которого поступила книга (абонент). Проведение отменено.'"), СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ПроверитьШапкуДокумента(Отказ);
	ОбщегоНазначенияСервер.ПроверитьНаОдинаковыеПозиции(Книги, Отказ);
	
	// Вернем книги
	Для Каждого ТекСтрокаКниги Из Книги Цикл
		// Вернем в библиотеку с библиотеки
		Движение = Движения.КнигиВБиблиотеке.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Книга = ТекСтрокаКниги.Книга;
		Движение.МестоРазмещения = Справочники.МестаРазмещения.Библиотека;
		Движение.Операция = Перечисления.Операция.ВозвратКнигиАбонентом;
		Движение.Количество = 1;
		// Спишем с абонента
		Движение = Движения.КнигиВБиблиотеке.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Книга = ТекСтрокаКниги.Книга;
		Движение.МестоРазмещения = Абонент;
		Движение.Операция = Перечисления.Операция.ВозвратКнигиАбонентом;
		Движение.Количество = 1;
	КонецЦикла;
	
	// Проверим что у этого абонента 0 книг
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КнигиВБиблиотекеОстатки.Книга,
		|	КнигиВБиблиотекеОстатки.КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.КнигиВБиблиотеке.Остатки(
		|			&Период,
		|			Книга В
		|					(ВЫБРАТЬ
		|						Документ.Книга
		|					ИЗ
		|						Документ.ВозвратКниг.Книги КАК Документ
		|					ГДЕ
		|						Документ.Ссылка = &Ссылка)
		|				И МестоРазмещения = &Абонент) КАК КнигиВБиблиотекеОстатки
		|ГДЕ
		|	КнигиВБиблиотекеОстатки.КоличествоОстаток = 1";

	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Абонент", Абонент);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	Отказ = Запрос.Выполнить().Пустой();

КонецПроцедуры

// Обработка заполнения от задачи ЗаказКниги
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		// Если заполняем документ из бизнес процесса заказа книг
		Если ТипЗнч(ДанныеЗаполнения) = Тип("ЗадачаСсылка.ЗаказКниги") Тогда
			Абонент = ДанныеЗаполнения.БизнесПроцесс.Автор.Абонент;
			Книги.Загрузить(ДанныеЗаполнения.БизнесПроцесс.КнигиКЗаказу.Выгрузить());
			Дата = ТекущаяДата();
			// Если заполняем документ из массива ссылка на книги
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Массив") Тогда
			Для каждого Книга Из ДанныеЗаполнения Цикл
				НоваяСтрока = Книги.Добавить();
				НоваяСтрока.Книга = Книга;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
